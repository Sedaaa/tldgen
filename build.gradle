apply from: "$sharedLocation/extrema.gradle"
apply from: "$sharedLocation/pgp.gradle"

project.version = tldgenVersion

repositories {
	flatDir name: 'jdk-home', dirs: System.properties['java.home'] + "/../lib/"
}

configurations {
	all*.exclude module: 'commons-logging'
	sources.exclude module: 'tools'
	sources.exclude group: 'net.sf.saxon', module: 'saxon'
}
 
dependencies {
	providedCompile	":tools",
					"net.sourceforge.saxon:saxon:9.1.0.8",
					"ch.qos.logback:logback-classic:0.9.24",
					"org.apache.geronimo.specs:geronimo-jsp_2.1_spec:1.0.1",
					"org.slf4j:slf4j-api:1.6.1",
			 		"commons-io:commons-io:1.4",
					"commons-lang:commons-lang:2.4"

	testCompile 	"junit:junit:4.5"

}

task jar(type: Jar, overwrite: true, dependsOn: compileJava, description: "Creates tldgen.jar") {
	from(sourceSets.main.classesDir) {
		include "org/tldgen/annotations/**"
	}
}

task toolsJar(type: Jar, dependsOn: compileJava, description: "Creates tldgen-tools.jar") {
	baseName = 'tldgen-tools';
	from sourceSets.main.classesDir
}

task zip(type: Zip, dependsOn: [jar, sourcesJar, javadocJar, toolsJar]) {
	prefix = "${project.name}-${project.version}/"
	into(prefix) {
		from(libsDir)
		from(projectDir) {
			include 'LICENSE' 
			include 'README'
		}
		from('src/main/scripts') {
			into 'bin'
			fileMode = 0755
		}
		from(docsDir) {
			into 'docs'
		}
		from(configurations.runtime.resolve() + configurations.providedCompile.resolve()) {
			into 'lib'
			exclude 'tools.jar'
		} 
	}
}
